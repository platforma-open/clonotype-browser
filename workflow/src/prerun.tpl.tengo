ll := import("@platforma-sdk/workflow-tengo:ll")
wf := import("@platforma-sdk/workflow-tengo:workflow")
smart := import("@platforma-sdk/workflow-tengo:smart")
assets := import("@platforma-sdk/workflow-tengo:assets")
render := import("@platforma-sdk/workflow-tengo:render")
pFrames := import("@platforma-sdk/workflow-tengo:pframes")
json := import("json")

util := import(":util")

doCalculationsTpl := assets.importTemplate(":do-calculations")
exportTableTpl := assets.importTemplate(":export-table")

wf.body(func(args) {
	if is_undefined(args.inputAnchor) {
		return {
			outputs: {
				tsv: smart.createNullResource()
			},
			exports: {}
		}
	}

	blockId := wf.blockId().getDataAsJson()

	outputs := {}

	if args.runExportAll {
		bundleBuilderForExport := wf.createPBundleBuilder()

		bundleBuilderForExport.addAnchor("main", args.inputAnchor)
		bundleBuilderForExport.addMulti({ axes: [{ anchor: "main", idx: 0 }, { anchor: "main", idx: 1 }] }, "perSample")
		bundleBuilderForExport.addMulti({ axes: [{ anchor: "main", idx: 1 }] }, "perClonotype")
		bundleBuilderForExport.addSingle({ name: "pl7.app/label", axes: [{ anchor: "main", idx: 0 }] }, "sampleLabels")
		
		// Add linker columns
		bundleBuilderForExport.addMulti({
			axes: [{ anchor: "main", idx: 1 }],
			annotations: { "pl7.app/isLinkerColumn": "true" },
			partialAxesMatch: true
		}, "linkers")

		// Add linked columns from args.tableInputs
		linkedColumns := {}
		linkedAxisCount := 0
		if !is_undefined(args.tableInputs) && !is_undefined(args.tableInputs.linkedColumns) {
			for anchorName, entry in args.tableInputs.linkedColumns {
				if !is_undefined(entry.anchorRef) && !is_undefined(entry.columns) {
					// Add anchor for this linker
					bundleBuilderForExport.addAnchor(entry.anchorName, entry.anchorRef)
					linkedColumns[anchorName] = {
						anchorRef: entry.anchorRef,
						columns: {},
						labels: {}
					}
					// Add each column from the columns map (query -> label)
					for colQueryStr, label in entry.columns {
						// Parse the JSON-serialized AnchoredPColumnSelector query
						colQuery := json.decode(colQueryStr)
						// Add the query to the bundle
						colName	:= "linkedAxis" + string(linkedAxisCount)
						bundleBuilderForExport.addSingle(colQuery, colName)
						linkedAxisCount += 1
						linkedColumns[anchorName].columns[colName] = colQuery
						// Store the label from model
						linkedColumns[anchorName].labels[colName] = label
					}
				}
			}
		}

		exportResult := render.createEphemeral(exportTableTpl, {
			columnBundle: bundleBuilderForExport.build(),
			inputAnchor: args.inputAnchor,
			linkedColumns: linkedColumns,
			byClonotypeLabels: args.tableInputs.byClonotypeLabels
		})

		outputs["tsvZip"] = exportResult.output("tsvZip")
		outputs["exportDebug"] = exportResult.output("exportDebug")
	}

	// outputs["aggregatesPf"] = pFrames.exportFrame(calculationResult.output("aggregatesPf"))

	if len(args.annotationScript.steps) > 0 {
		columnBundleForAnnotation := util.createColumnBundle(wf, args)
		calculationResult := render.createEphemeral(doCalculationsTpl, {
			blockId: blockId,
			blockArgs: args,
			columnBundle: columnBundleForAnnotation
		})
		outputs["annotationPf"] = pFrames.exportFrame(calculationResult.output("annotationPf"))
		outputs["statsPf"] = pFrames.exportFrame(calculationResult.output("statsPf"))
	}

	return {
		outputs: outputs,
		exports: {}
	}
})
